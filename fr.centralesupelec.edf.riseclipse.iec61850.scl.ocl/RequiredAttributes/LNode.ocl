/**
 *  Copyright (c) 2017 CentraleSupélec & EDF.
 *  All rights reserved. This program and the accompanying materials
 *  are made available under the terms of the Eclipse Public License v1.0
 *  which accompanies this distribution, and is available at
 *  http://www.eclipse.org/legal/epl-v10.html
 * 
 *  This file is part of the RiseClipse tool
 *  
 *  Contributors:
 *      Computer Science Department, CentraleSupélec
 *      EDF R&D
 *  Contacts:
 *      dominique.marcadet@centralesupelec.fr
 *      aurelie.dehouck-neveu@edf.fr
 *  Web site:
 *      http://wdi.supelec.fr/software/RiseClipse/
 */
import scl: 'http://www.iec.ch/61850/2003/SCL'

include '../Helpers/Enums.ocl'

package scl

context LNode
-- extends UnNaming

    -- The lnClass attribute shall be present.
    inv LNode_lnClass_required
        ( 'lnClass attribute shall be present in LNode (line ' + self.lineNumber.toString() + ')' )
    :
        self.lnClass <> null

    -- The lnClass attribute shall be valid.
    inv LNode_lnClass_value
        ( 'lnClass attribute shall have a valid value in LNode (line ' + self.lineNumber.toString() + '). '
          + 'Current value is ' + self.lnClass.toString()
        )
    :
        self.lnClass <> null implies self.validSclLNClassEnum( lnClass )

    -- The lnInst "Can only be missing for lnClass = LLN0, meaning as value here the empty string"
    inv LNode_lnInst_required_if_lnClass_not_LLN0
        ( 'lnInst attribute shall be present in LNode when lnClass <> LLN0 (line ' + self.lineNumber.toString() + ')' )
    :
        self.lnClass <> 'LLN0' implies self.lnInst <> null and self.lnInst <> ''

    -- The iedName attribute shall be valid if present.
    inv LNode_iedName_value
        ( 'iedName attribute shall have a valid value in LNode (line ' + self.lineNumber.toString() + '). '
          + 'Current value is ' + self.iedName.toString()
        )
    :
        self.iedName <> null implies self.validSclIEDNameOrNone( iedName )

    -- The ldInst attribute shall be valid if present.
    inv LNode_ldInst_value
        ( 'ldInst attribute shall have a valid value in LNode (line ' + self.lineNumber.toString() + '). '
          + 'Current value is ' + self.ldInst.toString()
        )
    :
        self.ldInst <> null implies self.validSclLDInstOrEmpty( ldInst )

    -- The prefix attribute shall be valid if present.
    inv LNode_prefix_value
        ( 'prefix attribute shall have a valid value in LNode (line ' + self.lineNumber.toString() + '). '
          + 'Current value is ' + self.prefix.toString()
        )
    :
        self.prefix <> null implies self.validSclPrefix( prefix )

    -- The lnInst attribute shall be valid if present.
    inv LNode_lnInst_value
        ( 'lnInst attribute shall have a valid value in LNode (line ' + self.lineNumber.toString() + '). '
          + 'Current value is ' + self.lnInst.toString()
        )
    :
        self.lnInst <> null implies self.validSclLNInstOrEmpty( lnInst )

    -- The lnType attribute shall be valid if present.
    inv LNode_lnType_value
        ( 'lnType attribute shall have a valid value in LNode (line ' + self.lineNumber.toString() + '). '
          + 'Current value is ' + self.lnType.toString()
        )
    :
        self.lnType <> null implies self.validSclName( lnType )


endpackage

