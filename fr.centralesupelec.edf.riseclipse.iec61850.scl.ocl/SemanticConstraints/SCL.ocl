/**
 *  Copyright (c) 2017 CentraleSupélec & EDF.
 *  All rights reserved. This program and the accompanying materials
 *  are made available under the terms of the Eclipse Public License v1.0
 *  which accompanies this distribution, and is available at
 *  http://www.eclipse.org/legal/epl-v10.html
 * 
 *  This file is part of the RiseClipse tool
 *  
 *  Contributors:
 *      Computer Science Department, CentraleSupélec
 *      EDF R&D
 *  Contacts:
 *      dominique.marcadet@centralesupelec.fr
 *      aurelie.dehouck-neveu@edf.fr
 *  Web site:
 *      http://wdi.supelec.fr/software/RiseClipse/
 */
import scl: 'http://www.iec.ch/61850/2003/SCL'

package scl

context SCL

    -- The substation name shall be unique within an SCL file.
    -- Note: Substation are contained in SCL or in Process
    inv SCL_unique_Substation_name
    ( 'All Substation within an SCL file shall have different name (line ' + self.lineNumber.toString() + ')' )
    :
        scl::Substation.allInstances()->isUnique( s : Substation | s.name )

    -- The IED name shall be unique within the SCL file.
    inv SCL_unique_IED_name
    ( 'All IED within an SCL file shall have different name (line ' + self.lineNumber.toString() + ')' )
    :
        scl::IED.allInstances()->isUnique( i : IED | i.name )

    inv SCL_unique_ConnectivityNode_pathName
    ( 'All ConnectivityNode within an SCL file shall have different pathName (line ' + self.lineNumber.toString() + ')' )
    :
        scl::ConnectivityNode.allInstances()->isUnique( c : ConnectivityNode | c.pathName )
    
    -- The LD name built from inst and other parts as described in 28H8.5 shall be unique within each
    -- SCL file.
    inv SCL_unique_LDevice_name
    ( 'All LDevice within an SCL file shall have different LD name built from inst and other parts (line ' + self.lineNumber.toString() + ')' )
    :
        scl::LDevice.allInstances() -> isUnique( l : LDevice | l.Server.AccessPoint.IED.name.concat( l.inst ))


endpackage



